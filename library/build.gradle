apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

ext.versionMajor = 1
ext.versionMinor = 1
ext.versionPatch = 1

android {
    compileSdkVersion 30

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 30
        versionCode versionMajor * 100000 + versionMinor * 100 + versionPatch * 1
        versionName "$versionMajor.$versionMinor.$versionPatch"
        setProperty("archivesBaseName", "expandable-fab")

        // Changes the name of generated AAR artifacts to be more descriptive
        libraryVariants.all { variant ->
            variant.outputs.all { output ->
                if (outputFileName.endsWith('.aar')) {
                    outputFileName = "${archivesBaseName}-${versionName}.aar"
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'com.google.android.material:material:1.4.0'
}

apply from: 'generate-docs-and-update-website.gradle'
apply from: 'publish-artifacts-to-maven-repo.gradle'
